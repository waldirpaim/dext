unit MainForm;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.ExtCtrls, Vcl.DBCtrls, Vcl.Grids,
  Vcl.DBGrids, Data.DB,Dext.Entity.Attributes, Vcl.Buttons,
  Dext.Collections, Dext.Core.Activator,  Dext.Entity.DataSet,
  FireDAC.Stan.Intf, FireDAC.Stan.Option, FireDAC.Stan.Error, FireDAC.UI.Intf, FireDAC.Phys.Intf,
  FireDAC.Stan.Def, FireDAC.Stan.Pool, FireDAC.Stan.Async, FireDAC.Phys, FireDAC.VCLUI.Wait,
  FireDAC.Comp.Client, FireDAC.Phys.SQLite, FireDAC.Phys.SQLiteDef,
  FireDAC.Stan.ExprFuncs, FireDAC.Phys.SQLiteWrapper.Stat;

type
//  Money = Currency;
  Money = Double;
  [Table('stock')]
  TStockItem = class
  private
    FId: Integer;
    FWarehouse: string;
    FQuantity: Double;
  public
    constructor Create(Id: Integer; const Warehouse: string; Qty: Double);
    [PK, DisplayLabel('Código')]
    property Id: Integer read FId write FId;
    [DisplayLabel('Depósito'), DisplayWidth(100)]
    property Warehouse: string read FWarehouse write FWarehouse;
    [DisplayLabel('Quantidade')]
    property Quantity: Double read FQuantity write FQuantity;
  end;

  [Table('products')]
  TProduct = class
  private
    FId: Integer;
    FDescription: string;
    FPrice: Money;
    FStock: IList<TStockItem>;
  public
    constructor Create(Id: Integer; const Description: string; Price: Money);
    destructor Destroy; override;
    [PK, DisplayLabel('Código')]
    property Id: Integer read FId write FId;
    [DisplayLabel('Descrição'), DisplayWidth(75), MaxLength(200)]
    property Description: string read FDescription write FDescription;
    [DisplayLabel('Valor'), Currency]
    property Price: Money read FPrice write FPrice;
    [Visible(False)]
    property Stock: IList<TStockItem> read FStock write FStock;
  end;

  TFormMain = class(TForm)
    DataSource: TDataSource;
    DataSourceDetail: TDataSource;
    DBGridDetail: TDBGrid;
    DBGridProducts: TDBGrid;
    DBNavigator: TDBNavigator;
    PanelTop: TPanel;
    RealMasterDetailButton: TSpeedButton;
    Splitter: TSplitter;
    procedure FormCreate(Sender: TObject);
    procedure RealMasterDetailButtonClick(Sender: TObject);
  private
    FDataSet: TEntityDataSet;
    FProducts: IList<TProduct>;
  end;

var
  FormMain: TFormMain;

implementation

uses
  Dext.Entity.Core,
  MasterDetailForm;

{$R *.dfm}

procedure TFormMain.FormCreate(Sender: TObject);
begin
  FDataSet := TEntityDataSet.Create(Self);
  DataSource.DataSet := FDataSet;
  DBGridProducts.DataSource := DataSource;

  FProducts := TCollections.CreateList<TProduct>(True);

  for var i := 0 to 99 do
  begin
    var LProduct := TProduct.Create(
      100 + i,
      'Product ' + IntToStr(i + 1),
      100.0 * (i + 1)
    );
    
    // Adicionando alguns itens de estoque para o mestre
    LProduct.Stock.Add(TStockItem.Create(i * 10 + 1, 'Warehouse A', (i + 1) * 10));
    LProduct.Stock.Add(TStockItem.Create(i * 10 + 2, 'Warehouse B', (i + 1) * 5));
    if i mod 2 = 0 then
    begin
      LProduct.Stock.Add(TStockItem.Create(i * 10 + 3, 'Warehouse C', (i + 1) * 15));
      LProduct.Stock.Add(TStockItem.Create(i * 10 + 4, 'Warehouse D', (i + 1) * 20));
    end;

    FProducts.Add(LProduct);
  end;

  // Carregando dados no DataSet
  FDataSet.Load(FProducts as IObjectList, TProduct);
  FDataSet.Open;

  // Vinculando o Detalhe nativamente via NestedDataSet
  DataSourceDetail.DataSet := (FDataSet.FieldByName('Stock') as TDataSetField).NestedDataSet;
end;

{ TStockItem }

constructor TStockItem.Create(Id: Integer; const Warehouse: string; Qty: Double);
begin
  inherited Create;
  FId := Id;
  FWarehouse := Warehouse;
  FQuantity := Qty;
end;

{ TProduct }

constructor TProduct.Create(Id: Integer; const Description: string; Price: Money);
begin
  inherited Create;
  FId := Id;
  FDescription := Description;
  FPrice := Price;
  FStock := TCollections.CreateList<TStockItem>(True);
end;

destructor TProduct.Destroy;
begin
  FStock := nil;
  inherited;
end;

procedure TFormMain.RealMasterDetailButtonClick(Sender: TObject);
begin
  with TFormMasterDetailReal.Create(Self) do
  try
    ShowModal;
  finally
    Free;
  end;
end;

initialization
  TActivator.RegisterDefault<IList<TStockItem>, TList<TStockItem>>;

end.
